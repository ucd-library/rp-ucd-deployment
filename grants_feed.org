* Grants Feed
:PROPERTIES:
:header-args:http: :host http://localhost:3030 :user admin:quinnisgreat
:header-args:sparqlx: :url http://sparql.org/sparql :format text/csv
:header-args:sparql: :url http://localhost:8081/private/sparql :format text/csv
:END:


Questions

What about multiple funders? Do they get added twice ?


** Prefixes

#+name: lob-ingest-file
#+BEGIN_SRC emacs-lisp :var file="prefixes.org"
(org-babel-lob-ingest file);
#+END_SRC

#+RESULTS: lob-ingest-file
: 0


#+CALL: lob-ingest-file(file="~/rp-ucd-harvest/lib/prefixes.org")

#+RESULTS:
: 2

** Description texts

#+begin_src sparql :noweb yes :format raw :wrap SRC ttl :url http://localhost:8081/private/sparql :var limit="1"
  <<prefixes>>
  describe ?grant
  where {
    {
      select ?grant WHERE {
        {
          select ?grant (count(*) as ?funder_count)
          WHERE {
            ?grant a vivo:Grant;
                   vivo:assignedBy ?funder;
                 } group by ?grant
        }
      }
    }

    ?grant a vivo:Grant;
           ucdrp:grantType ?type;
           vivo:dateTimeInterval [vivo:start/vivo:dateTime ?start; vivo:end/vivo:dateTime ?end];
           vivo:assignedBy ?funder;
           .

    } limit ?limit

#+end_src

#+RESULTS:
#+begin_SRC ttl
@prefix private: <http://experts.ucdavis.edu/private/> .
@prefix xsd:   <http://www.w3.org/2001/XMLSchema#> .
@prefix FoR:   <http://experts.ucdavis.edu/concept/FoR/> .
@prefix skos:  <http://www.w3.org/2004/02/skos/core#> .
@prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#> .
@prefix ucdrp: <http://experts.ucdavis.edu/schema#> .
@prefix purl:  <http://purl.org/ontology/bibo/> .
@prefix aeq:   <http://experts.ucdavis.edu/queries/schema#> .
@prefix iam:   <http://iam.ucdavis.edu/schema#> .
@prefix authorship: <http://experts.ucdavis.edu/authorship/> .
@prefix vivo:  <http://vivoweb.org/ontology/core#> .
@prefix free:  <http://experts.ucdavis.edu/concept/free> .
@prefix harvest_iam: <http://iam.ucdavis.edu/> .
@prefix foaf:  <http://xmlns.com/foaf/0.1/> .
@prefix oap:   <http://oapolicy.universityofcalifornia.edu/vocab#> .
@prefix work:  <http://experts.ucdavis.edu/work/> .
@prefix query: <http://experts.ucdavis.edu/schema/queries/> .
@prefix afn:   <http://jena.apache.org/ARQ/function#> .
@prefix harvest_oap: <http://oapolicy.universityofcalifornia.edu/> .
@prefix vcard: <http://www.w3.org/2006/vcard/ns#> .
@prefix q:     <http://experts.ucdavis.edu/queries/> .
@prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix person: <http://experts.ucdavis.edu/person/> .
@prefix bibo:  <http://purl.org/ontology/bibo/> .
@prefix grant: <http://experts.ucdavis.edu/grant/> .
@prefix experts: <http://experts.ucdavis.edu/> .
@prefix obo:   <http://purl.obolibrary.org/obo/> .
#+end_SRC

*** Grant Feed

#+begin_src sparql :noweb yes :url http://localhost:8081/private/sparql :var limit="10000" :results file :file grants.csv
  <<prefixes>>
  PREFIX grant: <http://experts.ucdavis.edu/grant/>

  select ?id ?category (?grant as ?url) ?institution_reference ?title ?type ?funding_type ?start_date ?end_date ?funder ?reference ?amount ?visible
  where {
    # Here we limit grants to people in our system
    {
      SERVICE <http://fuseki:3030/experts/sparql> {
        select distinct ?expert WHERE { ?expert a ucdrp:person. }
      }
    }

    ?grant a vivo:Grant;
           ucdrp:grantType/rdfs:label ?funding_type;
           rdfs:label ?title;
           vivo:dateTimeInterval [
                                   vivo:start/vivo:dateTime ?start_date;
                                   vivo:end/vivo:dateTime ?end_date;
                                 ];
    vivo:relates [
                   a ucdrp:GrantPrincipalInvestigatorRole;
                   obo:RO_000052 ?expert;
                 ];
    .

    OPTIONAL {
      ?grant vivo:assignedBy/rdfs:label ?funder;
             vivo:sponsorAwardId ?reference;
             .
    }
    OPTIONAL {
      ?grant vivo:totalAwardAmount ?amount
      .
    }
    bind(replace(str(?grant),str(grant:),'') as ?institution_reference)
    bind(replace(str(?grant),str(grant:),"davis-") as ?id)
    bind("grant" as ?category)
    bind("c-davis" as ?type)
    bind("TRUE" as ?visible)

  } limit $limit
  #}
#+end_src

#+RESULTS:
[[file:grants.csv]]


*** Grant Links Feed

    What are our roles again?
#+begin_src sparql :noweb yes :url http://localhost:8081/private/sparql
  <<prefixes>>
  select ?role ?label
  where {
    ?role a vivo:ResearcherRole;
          rdfs:label ?label;
          ucdrp:abbrev ?abbrev;
            .
  }
#+end_src

#+RESULTS:
| role                                                               | label                     |
|--------------------------------------------------------------------+---------------------------|
| http://experts.ucdavis.edu/schema#GrantOtherRole                   | Researcher                |
| http://experts.ucdavis.edu/schema#GrantProjectLeaderRole           | Project Leader            |
| http://experts.ucdavis.edu/schema#GrantCoPrincipalInvestigatorRole | Co-Principal Investigator |
| http://experts.ucdavis.edu/schema#GrantProgramDirectorRole         | Program Director          |
| http://experts.ucdavis.edu/schema#GrantCoreLeaderRole              | Core Leader               |
| http://experts.ucdavis.edu/schema#GrantKeyPersonnelRole            | Key Personnel             |
| http://experts.ucdavis.edu/schema#GrantPrincipalInvestigatorRole   | Principal Investigator    |

This is a quick review of the people in our system

#+begin_src sparql :noweb yes :url http://localhost:8081/private/sparql
      <<prefixes>>
  #  select (count(*) as ?cnt) WHERE {
  select (group_concat(?cas) as ?casList) WHERE {
    select ?cas
      where {
        SERVICE <http://fuseki:3030/experts/sparql> {
          ?person a ucdrp:person;
                  ucdrp:casId ?cas;
                  .
        }
      } order by ?cas
  }
#+end_src


 //#+begin_src sparql :noweb yes :format raw :wrap SRC ttl :url http://localhost:8081/private/sparql :var limit="10"
#+begin_src sparql :noweb yes :url http://localhost:8081/private/sparql :var limit="100000" :results file :file links.csv
              <<prefixes>>
      select ?category-1 ?id-1 ?category-2 ?id-2 ?link-type-id ("TRUE" as ?visible)
      WHERE {
        # Here we select the grants that interest us
        {
          select ?grant where {
            {
              SERVICE <http://fuseki:3030/experts/sparql> {
                select distinct ?expert WHERE {
                  ?expert a ucdrp:person.
                }
              }
            }
            ?grant a vivo:Grant;
                   vivo:relates ?relates;
                   .
            ?relates a ucdrp:GrantPrincipalInvestigatorRole;
                     obo:RO_000052 ?expert;
                   }
        }

        {
          ?grant vivo:relates ?relates.
          ?relates a ?role;
                  obo:RO_000052/ucdrp:casId ?person;
                  obo:RO_000052/ucdrp:employeeId ?employeeid
                  .
          bind("user" as ?category-1)
          bind("grant" as ?category-2)
          bind(?employeeid as ?id-1)
          #      bind(concat(?person,"@ucdavis.edu") as ?id-1)
          bind(replace(str(?grant),str(grant:),"davis-") as ?id-2)

          VALUES (?role ?link-type-id ?e-role) {
            (ucdrp:GrantAccountManagerRole "183" "Researcher on" )
            (ucdrp:GrantOtherRole "183" "Researcher on" )
            (ucdrp:GrantProjectLeaderRole "118" "Project Lead of")
            (ucdrp:GrantCoPrincipalInvestigatorRole "116" "Co-PI of")
            (ucdrp:GrantProgramDirectorRole "137" "Program Director of")
            (ucdrp:GrantCoreLeaderRole "119" "Co leader on")
            (ucdrp:GrantKeyPersonnelRole "97" "Senior personal of")
            (ucdrp:GrantPrincipalInvestigatorRole "43" "Primary investagor for")
          }
        } UNION {
          ?grant vivo:relates ?relates.
          ?relates a ?role;
                   obo:RO_000052/ucdrp:casId ?person;
                  obo:RO_000052/ucdrp:employeeId ?employeeid
                   .
          bind("grant" as ?category-1)
          bind("user" as ?category-2)
          bind(replace(str(?grant),str(grant:),"davis-") as ?id-1)
          bind(?employeeid as ?id-2)
    #      bind(concat(?person,"@ucdavis.edu") as ?id-2)
          bind("17" as ?link-type-id)
          bind("Funder of" as ?e-role)
        }
      } limit ?limit
#+end_src

#+RESULTS:
[[file:links.csv]]



* Sympletic Example

** Data
   This is the original grants example that we will try with sympletic.   It has
   19 grants selected by Vessela, plus all the grants from Ted Grosholz.


 // #+begin_src sparql :noweb yes :format raw :wrap SRC ttl :var limit="1"
   #+name: grants_test
   #+begin_src sparql :noweb yes :var limit="100" :results file :file grants_test.csv
     PREFIX FoR: <http://experts.ucdavis.edu/concept/FoR/>
     PREFIX aeq: <http://experts.ucdavis.edu/queries/schema#>
     PREFIX afn: <http://jena.apache.org/ARQ/function#>
     PREFIX authorship: <http://experts.ucdavis.edu/authorship/>
     PREFIX bibo: <http://purl.org/ontology/bibo/>
     PREFIX experts: <http://experts.ucdavis.edu/>
     PREFIX foaf: <http://xmlns.com/foaf/0.1/>
     PREFIX free: <http://experts.ucdavis.edu/concept/free>
     PREFIX grant: <http://experts.ucdavis.edu/grant/>
     PREFIX harvest_iam: <http://iam.ucdavis.edu/>
     PREFIX harvest_oap: <http://oapolicy.universityofcalifornia.edu/>
     PREFIX iam: <http://iam.ucdavis.edu/schema#>
     PREFIX oap: <http://oapolicy.universityofcalifornia.edu/vocab#>
     PREFIX obo: <http://purl.obolibrary.org/obo/>
     PREFIX person: <http://experts.ucdavis.edu/person/>
     PREFIX private: <http://experts.ucdavis.edu/private/>
     PREFIX purl: <http://purl.org/ontology/bibo/>
     PREFIX q: <http://experts.ucdavis.edu/queries/>
     PREFIX query: <http://experts.ucdavis.edu/schema/queries/>
     PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
     PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
     PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
     PREFIX ucdrp: <http://experts.ucdavis.edu/schema#>
     PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
     PREFIX vivo: <http://vivoweb.org/ontology/core#>
     PREFIX work: <http://experts.ucdavis.edu/work/>
     PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

     select ?id ?category (?grant as ?url) ?institution_reference ?title ?type ?funding_type ?start_date ?end_date ?funder ?reference ?amount ?visible
           where {
             {
               {
               select ?grant ?l
                 where {
                   graph ?g {
                     bind(uri(concat(str(person:),md5("grosholz"))) as ?ted)
                     ?grant a vivo:Grant;
                            rdfs:label ?title;
                            vivo:relates/obo:RO_000052 ?ted;
                            .
                   }}
               } UNION {
             values (?grant ?l) {
               (grant:9504 "(UColorado/Knoesen/inactive)")
               (grant:115073 "(NORTH CAROLINA STATE UNIVERSITY/Chowdhury/inactive)")
               (grant:102948 "(NSF/Tagkopoulos/inactive)")
               (grant:108668 "(NSF/Bai/Devanbu/inactive)")
               (grant:4357 "(NASA/Aldredge/inactive)")
               (grant:102410 "(NSF/Amenta/inactive)")
               (grant:12831 "(STATE WATER RESOURCES CONTROL BOARD/ Lund/inactive)")
               (grant:126530 "(Koret/Aldredge/active)")
               (grant:126597 "(NATIONAL INSTITUTE FOR FOOD AND AGRICULTURE/ Bornhorst Jenkins, Donis-Gonzalez/active)")
               (grant:127114 "(NSF/Miller,Jenkins,Kendall/active)")
               (grant:123693 "(Mozilla/Amenta, Devsanbu, Neff, Liu, others/inactive)")
               (grant:127578 "(Betteravia/Stavros/inactive)")
               (grant:129164 "(ABC/Stavros/active)")
               (grant:122318 "Barbato grant")
               (grant:126224 "Barbato grant")
               (grant:111109 "Hart grant")
               (grant:111052 "Hart grant")
               (grant:104120 "Hart grant")
             }
               }
             }

             graph ?g {
             ?grant a vivo:Grant;
                    ucdrp:grantType/rdfs:label ?funding_type;
                    rdfs:label ?title;
                    vivo:dateTimeInterval [
                                            vivo:start/vivo:dateTime ?start_date;
                                            vivo:end/vivo:dateTime ?end_date;
                                          ];
             vivo:relates [
                            a ucdrp:GrantPrincipalInvestigatorRole;
                            obo:RO_000052 ?expert;
                          ];
             .

             OPTIONAL {
               ?grant vivo:assignedBy/rdfs:label ?funder;
                      vivo:sponsorAwardId ?reference;
                      .
             }
             OPTIONAL {
               ?grant vivo:totalAwardAmount ?amount
               .
             }
             bind(replace(str(?grant),str(grant:),'') as ?institution_reference)
             bind(replace(str(?grant),str(grant:),"davis-") as ?id)
             bind("grant" as ?category)
             bind("c-davis" as ?type)
             bind("TRUE" as ?visible)

             }
           } limit $limit
   #+end_src

   #+RESULTS: grants_test
   [[file:grants_test.csv]]

   #+name: grants_test_links
   #+begin_src sparql :noweb yes :var limit="1000" :results file :file links_test.csv
       <<prefixes>>
   select ?category_1 ?id_1 ?category_2 ?id_2 ?link_type_id ("TRUE" as ?visible)
   WHERE {
     graph ?g {
       {
         select ?grant ?l
         where {
           bind(uri(concat(str(person:),md5("grosholz"))) as ?ted)
           ?grant a vivo:Grant;
                  rdfs:label ?title;
                  vivo:relates/obo:RO_000052 ?ted;
                  .
         }
       } UNION {
         values (?grant ?l) {
           (grant:9504 "(UColorado/Knoesen/inactive)")
           (grant:115073 "(NORTH CAROLINA STATE UNIVERSITY/Chowdhury/inactive)")
           (grant:102948 "(NSF/Tagkopoulos/inactive)")
           (grant:108668 "(NSF/Bai/Devanbu/inactive)")
           (grant:4357 "(NASA/Aldredge/inactive)")
           (grant:102410 "(NSF/Amenta/inactive)")
           (grant:12831 "(STATE WATER RESOURCES CONTROL BOARD/ Lund/inactive)")
           (grant:126530 "(Koret/Aldredge/active)")
           (grant:126597 "(NATIONAL INSTITUTE FOR FOOD AND AGRICULTURE/ Bornhorst Jenkins, Donis-Gonzalez/active)")
           (grant:127114 "(NSF/Miller,Jenkins,Kendall/active)")
           (grant:123693 "(Mozilla/Amenta, Devsanbu, Neff, Liu, others/inactive)")
           (grant:127578 "(Betteravia/Stavros/inactive)")
           (grant:129164 "(ABC/Stavros/active)")
           (grant:122318 "Barbato grant")
           (grant:126224 "Barbato grant")
           (grant:111109 "Hart grant")
           (grant:111052 "Hart grant")
           (grant:104120 "Hart grant")
         }
       }

       {
         ?grant vivo:relates ?relates.
         ?relates a ?role;
                  obo:RO_000052 ?person;
                  .
         graph <http://iam.ucdavis.edu/> {
           ?person ucdrp:casId ?casId;
                   ucdrp:employeeId ?employeeid;
                   .
         }

         bind("user" as ?category_1)
         bind("grant" as ?category_2)
         bind(?employeeid as ?id_1)
         bind(replace(str(?grant),str(grant:),"davis-") as ?id_2)

         VALUES (?role ?link_type_id ?e_role) {
           (ucdrp:GrantAccountManagerRole "183" "Researcher on" )
           (ucdrp:GrantOtherRole "183" "Researcher on" )
           (ucdrp:GrantProjectLeaderRole "118" "Project Lead of")
           (ucdrp:GrantCoPrincipalInvestigatorRole "116" "Co-PI of")
           (ucdrp:GrantProgramDirectorRole "137" "Program Director of")
           (ucdrp:GrantCoreLeaderRole "119" "Co leader on")
           (ucdrp:GrantKeyPersonnelRole "97" "Senior personal of")
           (ucdrp:GrantPrincipalInvestigatorRole "43" "Primary investagor for")
         }
       } UNION {
         ?grant vivo:relates ?relates.
         ?relates a ?role;
                  obo:RO_000052 ?person;
                  .
         graph <http://iam.ucdavis.edu/> {
           ?person ucdrp:casId ?casId;
                   ucdrp:employeeId ?employeeid;
                   .
         }

         bind("grant" as ?category_1)
         bind("user" as ?category_2)
         bind(replace(str(?grant),str(grant:),"davis-") as ?id_1)
         bind(?employeeid as ?id_2)
         bind("17" as ?link_type_id)
         bind("Funder of" as ?e_role)
       }
     }
   } limit $limit

 #+end_src

 #+RESULTS: grants_test_links
 [[file:links_test.csv]]

** sFTP

   We are planning on using sympletic's sftp server to update our data.
